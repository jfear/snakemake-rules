# -*- snakemake -*-
config_default = {
    'bio.ngs.align.bowtie' : {
        'align' : {
            'threads': config['settings']['threads'],
            'options': '--chunkmbs 200',
        },
    },
}

update_config(config_default, config)
config = config_default

rule bowtie_align_pe:
    """Bowtie paired end alignment"""
    params: cmd = config['bio.ngs.align.bowtie']['cmd'],
            options = config['bio.ngs.align.bowtie']['align']['options'],
            index = config['bio.ngs.align.bowtie']['index'],
            samtools = config['bio.ngs.tools.samtools']['cmd']
    input: read1 = "{prefix}" + config['bio.ngs.settings']['read1_label'] + config['bio.ngs.settings']['fastq_suffix'],\
           read2 = "{prefix}" + config['bio.ngs.settings']['read2_label'] + config['bio.ngs.settings']['fastq_suffix'],\
           index = expand("{index}{ext}", index=config['bio.ngs.align.bowtie']['index'], ext=config['bio.ngs.align.bowtie']['build_ext'])
    output: bam = "{prefix}.bam"
    benchmark: "{prefix}.json"
    threads: config['bio.ngs.align.bowtie']['align']['threads']
    log: "{prefix}.bwt.log"
    shell:
        "if mimetype -b {input.read1} | grep -q zip; then "
        "{params.cmd} -S -p {threads} {params.options} {params.index} -1 <(gunzip -c {input.read1}) -2 <(gunzip -c {input.read2}) 2> {log} | {params.samtools} view -bS - > {output.bam}; "
        " else "
        "{params.cmd} -S -p {threads} {params.options} {params.index} -1 {input.read1} -2 {input.read2} 2> {log} | {params.samtools} view -bS - > {output.bam};"
        " fi"
